# vim: set ft=zsh :

autoload zkbd
function zkbd_file() {
    [[ -f ~/.zsh/kbd/${TERM}-${VENDOR}-${OSTYPE} ]] && printf '%s' ~/".zsh/kbd/${TERM}-${VENDOR}-${OSTYPE}" && return 0
    [[ -f ~/.zsh/kbd/${TERM}-${DISPLAY}          ]] && printf '%s' ~/".zsh/kbd/${TERM}-${DISPLAY}"          && return 0
    return 1
}

[[ ! -d ~/.zsh/kbd ]] && mkdir ~/.zsh/kbd

if [[ ! -h ~/.zkbd ]]; then
    [[ -d ~/.zkbd ]] && mv ~/.zkbd/* ~/.zsh/kbd/
    rm -rf ~/.zkbd
    ln -s ~/.zsh/kbd ~/.zkbd
fi

keyfile=$(zkbd_file)
ret=$?
if [[ ${ret} -ne 0 ]]; then
    zkbd
    keyfile=$(zkbd_file)
    ret=$?
fi
if [[ ${ret} -eq 0 ]] ; then
    source "${keyfile}"
else
    printf 'Failed to setup keys using zkbd.\n'
fi
unfunction zkbd_file; unset keyfile ret

# setup key accordingly
[[ -n "${key[Home]}"    ]]  && bindkey  "${key[Home]}"    beginning-of-line
[[ -n "${key[End]}"     ]]  && bindkey  "${key[End]}"     end-of-line
[[ -n "${key[Home]}"    ]]  && bindkey -M vicmd "${key[Home]}"    beginning-of-line
[[ -n "${key[End]}"     ]]  && bindkey -M vicmd "${key[End]}"     end-of-line
[[ -n "${key[Insert]}"  ]]  && bindkey  "${key[Insert]}"  overwrite-mode
[[ -n "${key[Delete]}"  ]]  && bindkey  "${key[Delete]}"  delete-char
[[ -n "${key[Up]}"      ]]  && bindkey  "${key[Up]}"      up-line-or-history
[[ -n "${key[Down]}"    ]]  && bindkey  "${key[Down]}"    down-line-or-history
[[ -n "${key[Left]}"    ]]  && bindkey  "${key[Left]}"    backward-char
[[ -n "${key[Right]}"   ]]  && bindkey  "${key[Right]}"   forward-char

[[ -n "${key[PageUp]}"   ]]  && bindkey  "${key[PageUp]}"   history-search-backward
[[ -n "${key[PageDown]}"   ]]  && bindkey  "${key[PageDown]}"   history-search-forward

bindkey -M vicmd '^R' redo
bindkey -M vicmd 'u'  undo
bindkey -M vicmd 'ga' what-cursor-position
bindkey -M vicmd 'q'  push-line

bindkey '^xp' history-beginning-search-forward
bindkey '^xn' history-beginning-search-backward
bindkey '^X^I' expand-or-complete-prefix
bindkey '^X^H' expand-history

bindkey '^o' accept-and-infer-next-history
